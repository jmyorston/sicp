# Exercise 2.41:

Write a procedure to find all ordered triples of distinct positive integers $i$, $j$, and $k$ less than or equal to a given integer $n$ that sum to a given integer $s$.

## Answer

```scheme
; -------------------------------------
;           Exercise 2.41
; -------------------------------------
(displayln "*** Exercise 2.41 ***")

; procedure to create triplets
(define (unique-triplets n)
  (flatmap (λ (i)
             (map (λ (jk-pair) (append jk-pair (list i)))
                  (flatmap (λ (j)
                             (map (λ (k) (list k j))
                                  (enumerate-interval 1 (- j 1))))
                           (enumerate-interval 1 (- i 1)))))
           (enumerate-interval 1 n)))

; procedure to sum up the triplet
(define (sum-triplet triplet)
  (+ (car triplet) (cadr triplet) (cadr (cdr triplet))))

; procedure to append the sum of thee triplet to the list
(define (make-trip-sum trip)
  (append trip (list (sum-triplet trip))))

; procedure to check whether the sum of the triplet is equal to a given value
(define (trip-sum-equal-s s)
  (λ (triplet)(= (sum-triplet triplet)s)))

; putting all the procedures together
(define (order-trips n s)
  (map make-trip-sum
       (filter (trip-sum-equal-s s)
               (unique-triplets n))))

(order-trips 6 6)
 ; '((1 2 3 6))
```
