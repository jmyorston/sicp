# Exercise 1.32
Show that `sum` and `product` [(exercise 1.31)](./ex1-31.md) are both special cases of a still more general notion called `accumulate` that combines a collection of terms, using some general accumulation function:

`(accumulate combiner null-value term a next b)`

`accumulate` takes as arguments the same term and range specifications as `sum` and `product`, together with a combiner procedure (of two arguments) that specifies how the current term is to be combined with the accumulation of the preceding terms and a `null-value` that specifies what base value to use when the terms run out. Write `accumulate` and show how `sum` and `product` can both be defined as simple calls to `accumulate`.

Write two procedures, one that generates a recursive process and one iterative.

### Recursive
```scheme
(define (accumulate combiner null-value term a next b)
  (if (> a b)
      null-value
      (combiner (term a) (accumulate combiner null-value term (next a) next b))))

(define (inc x)(+ x 1))


; sum
(define (sum-acc term a next b)
  (accumulate + 0 term a next b))

(define (prod-acc term a next b)
  (accumulate * 1 term a next b))

; sum of squares between 2 and 5
(displayln (sum-acc (lambda (x) (* x x)) 2 inc 5))
; 54

; product of squares between 2 and 5
(displayln (prod-acc (lambda (x) (* x x)) 2 inc 5))
; 14400

```

### Iterative
```scheme

; accumulate iter
(define (acc-iter combiner null-value term a next b)
  (define (iter a result)
    (if (> a b)
        result
        (iter (next a) (combiner result (term a)))))
  (iter a null-value))

; sum
(define (sum-iter term a next b)
  (acc-iter + 0 term a next b))

(displayln (sum-iter (lambda (x) (* x x)) 2 inc 5))

; product
(define (prod-iter term a next b)
  (acc-iter * 1 term a next b))

(displayln (prod-iter (lambda (x) (* x x)) 2 inc 5))

```
