# Exercise 1.23
The `smallest-divisor` procedure shown at the start of this section does lots of needless testing: 
After it checks to see if the number is divisible by 2 there is no point in checking to see if it is divisible by any larger even numbers. 
This suggests that the values used for test-divisor should not be 2,3,4,5,6,…, but rather 2,3,5,7,9,…


To implement this change, define a procedure next that returns 3 if its input is equal to 2 and otherwise returns its input plus 2. Modify the smallest-divisor procedure to use (next test-divisor) instead of (+ test-divisor 1). With timed-prime-test incorporating this modified version of smallest-divisor, run the test for each of the 12 primes found in exercise 1.22. 

Since this modification halves the number of test steps, you should expect it to run about twice as fast. 
Is this expectation confirmed? If not, what is the observed ratio of the speeds of the two algorithms, and how do you explain the fact that it is different from 2?

### Answer
Theoretically, the updated procdure should take 50% of the time, as the number of possible values is halfed, however in the tests below we can see that it is more like 75%, this is because there is a performance cost calling the `next` procedure with the `if` statement.


```scheme
#lang racket

(define (report-prime-repeat prime? n repeat)
  (define (prime-repeat repeat-count)
    (cond ((= 0 repeat-count)
           (prime? n))
          (else
           (prime? n)
           (prime-repeat (- repeat-count 1)))))
  (display "    ")
  (display n)
  (let ((start-time (current-inexact-milliseconds)))
    (prime-repeat repeat)
    (let ((end-time (current-inexact-milliseconds)))
      (display " *** Time: ")
      (display (- end-time start-time))
      (newline))))

(define (find-divisor n test-divisor)
  (cond ((> (square test-divisor) n) n)
        ((divides? test-divisor n) test-divisor)
        (else (find-divisor n (next test-divisor)))))

(define (divides? a b)
  (= (remainder b a) 0))

(define (smallest-divisor n)
  (find-divisor n 2))

(define (prime? n)
  (= n (smallest-divisor n)))

(define (next n)
  (if (= n 2) 3 (+ n 2)))

(define (search-for-primes even-start count)
  (display "First three primes above ")
  (display even-start)
  (display ":")
  (newline)
  (define (iter candidate count)
    (cond ((= count 0) '())
          (else
           (cond ((prime? candidate)
                  (report-prime-repeat prime? candidate 3)
                  (iter (+ candidate 2) (- count 1)))
                 (else
                  (iter (+ candidate 2) count))))))
  (iter (+ even-start 1) count)
  (newline))

(define (square x)(* x x))

(search-for-primes 1000 3)
(search-for-primes 10000 3)
(search-for-primes 100000 3)
(search-for-primes 1000000 3)

```
#### without modified procedure
```
First three primes above 1000:
    1009 *** Time: 0.010986328125
    1013 *** Time: 0.010986328125
    1019 *** Time: 0.010986328125

First three primes above 10000:
    10007 *** Time: 0.033935546875
    10009 *** Time: 0.033935546875
    10037 *** Time: 0.033935546875

First three primes above 100000:
    100003 *** Time: 0.10205078125
    100019 *** Time: 0.10205078125
    100043 *** Time: 0.10107421875

First three primes above 1000000:
    1000003 *** Time: 0.320068359375
    1000033 *** Time: 0.317138671875
    1000037 *** Time: 0.321044921875
```
#### with modified next procedure
```
First three primes above 1000:
    1009 *** Time: 0.010009765625
    1013 *** Time: 0.009033203125
    1019 *** Time: 0.009033203125

First three primes above 10000:
    10007 *** Time: 0.026123046875
    10009 *** Time: 0.025146484375
    10037 *** Time: 0.026123046875

First three primes above 100000:
    100003 *** Time: 0.078125
    100019 *** Time: 0.078125
    100043 *** Time: 0.090087890625

First three primes above 1000000:
    1000003 *** Time: 0.248046875
    1000033 *** Time: 0.2490234375
    1000037 *** Time: 0.2529296875
```