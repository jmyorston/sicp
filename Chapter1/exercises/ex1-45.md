# Exercise 1.45
We saw in section 1.3.3 that attempting to compute square roots by naively finding a fixed point of $y \mapsto x/y$ does not converge, and that this can be fixed by average damping. 

The same method works for finding cube roots as fixed points of the average-damped $y \mapsto x / y^2$. Unfortunately, the process does not work for fourth roots - a single average damp is not enough to make a fixed-point search for $y \mapsto x / y^3$ converge. 

On the other hand, if we average damp twice (i.e., use the average damp of the average damp of $y \mapsto x / y^3$), the fixed-point search does converge. Do some experiments to determine how many average damps are required to compute $n{\th}$ roots as a fixed-point search based upon repeated average damping of $y \mapsto x / y^{n-1}$. 

Use this to implement a simple procedure for computing $n{\th}$ roots using fixed-point, `average-damp`, and the `repeated` procedure of exercise [1.43](./ex1-43.md). Assume that any arithmetic operations you need are available as primitives.

---
## Answer
```scheme
#lang racket

(define tolerance 0.00001)

(define (fixed-point f first-guess)
  (define (close-enough? v1 v2)
    (< (abs (- v1 v2)) tolerance))
  (define (try guess)
    (display guess) (newline)
    (let ((next (f guess)))
      (if (close-enough? guess next)
          next
          (try next))))
  (try first-guess))

(define (average-damp f)
  (λ (x) (average x (f x))))

(define (average x y)
  (/ (+ x y) 2))

(define (compose f g)
  (λ (x)
    (f (g x))))

(define (repeated f n)
  (if (= n 1)
      f
      (compose f (repeated f (- n 1)))))

(define (power x n)
  (if (= n 1)
      x
      (* x (power x (- n 1)))))

(define (nth-root x nth)
  (fixed-point 
   ((repeated average-damp (floor (log nth 2))) 
    (λ (y) 
      (/ x (power y (- nth 1)))))
   1.0))

(display (nth-root 2 258))
; 1.0
; 1.00390625
; 1.0028531987092006
; 1.0026923386149083
; 1.0026902132630033
```